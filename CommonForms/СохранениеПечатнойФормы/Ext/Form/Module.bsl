#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ИсторияСохранения_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ОбъектыПечати.Количество() > 0 Тогда
		ОбъектПечати = Параметры.ОбъектыПечати.ВыгрузитьЗначения()[0];
		КлючНастройки = Метаданные.НайтиПоТипу(ТипЗнч(ОбъектПечати)).Имя;
	Иначе
		КлючНастройки = "ОбщиеОбъекты";
	КонецЕсли;
	
	СтруктураПутей = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиХраненияФайлов(),
		ИмяКлючНастройкиХраненияФайлов(),
		Неопределено);
		
	Если СтруктураПутей = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ СтруктураПутей.Свойство(КлючНастройки) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ПапкаДляСохраненияФайлов.СписокВыбора.ЗагрузитьЗначения(СтруктураПутей[КлючНастройки]);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияСохранения_ПриОткрытииПосле(Отказ)
	
	МассивСохраненныхПутей = Элементы.ПапкаДляСохраненияФайлов.СписокВыбора.ВыгрузитьЗначения();
	Если МассивСохраненныхПутей.Количество() = 0 Тогда
		ЗаполнитьИсториюВыбораПоУмолчанию();
	Иначе
		// Выберем первый путь из сохраненных значений
		ВыбраннаяПапка = МассивСохраненныхПутей[0];
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИсторияСохранения_СохранитьПеред(Команда)
	
	ИсторияСохранения_СохранитьПередНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИсторияСохранения_СохранитьПередНаСервере()
	
	СтруктураПутей = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиХраненияФайлов(),
		ИмяКлючНастройкиХраненияФайлов(),
		Новый Структура);
		
	Если СтруктураПутей.Свойство(КлючНастройки) Тогда
		МассивПутей = СтруктураПутей[КлючНастройки];
	Иначе
		МассивПутей = Новый Массив;
	КонецЕсли;
	
	Для ОбратныйИндекс = -МассивПутей.ВГраница() По 0 Цикл
		// Удалим выбранный путь из массива, чтобы потом переставить его вначало
		Если МассивПутей[-ОбратныйИндекс] = ВыбраннаяПапка Тогда
			МассивПутей.Удалить(-ОбратныйИндекс);
			// Считаем, что в массиве у нас только уникальные пути, поэтому не продолжаем дальнейший поиск
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Последний использованный путь всегда становится первым, вытесняя последующие
	МассивПутей.Вставить(0, ВыбраннаяПапка);
	ОграничитьМассивПутей(МассивПутей);
	СтруктураПутей.Вставить(КлючНастройки, МассивПутей);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ИмяНастройкиХраненияФайлов(),
		ИмяКлючНастройкиХраненияФайлов(),
		СтруктураПутей);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИсториюВыбораПоУмолчанию()
	
	МассивЗначений = Элементы.ПапкаДляСохраненияФайлов.СписокВыбора.ВыгрузитьЗначения();
	МассивЗначений.Добавить(КаталогДокументов());
	Если Не ПустаяСтрока(ВыбраннаяПапка) Тогда
		МассивЗначений.Добавить(ВыбраннаяПапка);
	КонецЕсли;
	ВыбраннаяПапка = МассивЗначений[0]; // Установим первый элемент
	Элементы.ПапкаДляСохраненияФайлов.СписокВыбора.ЗагрузитьЗначения(МассивЗначений);
	
КонецПроцедуры

&НаСервере
Процедура ОграничитьМассивПутей(МассивПутей)
	
	КоличествоЭлементовВМассиве = МассивПутей.Количество();
	КоличествоОбъектовИстории = КоличествоОбъектовИстории();
	Если КоличествоЭлементовВМассиве <= КоличествоОбъектовИстории Тогда
		Возврат;
	КонецЕсли;
	
	Для ОбратныйИндекс = -МассивПутей.ВГраница() По 0 Цикл
		// Удалим все элементы истории, с индексом больше чем мы храним
		Если -ОбратныйИндекс > КоличествоОбъектовИстории-1 Тогда
			МассивПутей.Удалить(-ОбратныйИндекс);
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция КоличествоОбъектовИстории()

	Возврат 5;

КонецФункции

&НаСервере
Функция ИмяНастройкиХраненияФайлов()

	Возврат "Расширение_ХранениеПутейКФайлам";

КонецФункции

&НаСервере
Функция ИмяКлючНастройкиХраненияФайлов()

	Возврат "МассивПутей";

КонецФункции

#КонецОбласти